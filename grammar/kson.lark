?start: value+

?value: object
      | array
      | string
      | astring
      | bstring
      | number
      | nan
      | inf
      | minus_inf
      | true
      | false
      | null

object : "{" [ ","? object_entry ("," object_entry)* ","? ] "}"
array  : "[" [ ","? value ("," value)* ","? ] "]"
object_entry   : string ":" value

nan : "nan" | "NaN"
inf : "inf" | "Infinity"
minus_inf : "-inf" | "-Infinity"
number: SIGNED_NUMBER

true: "true"
false: "false"
null: "null"

ESCAPED_STRING_DOUBLE : "\"" _STRING_ESC_INNER "\""
ESCAPED_STRING_SINGLE : "'" _STRING_ESC_INNER "'"

string : ESCAPED_STRING_SINGLE | ESCAPED_STRING_DOUBLE

ASTRING_SINGLE : "a'" /.*?'/
ASTRING_DOUBLE : "a\"" /.*?\"/

astring : ASTRING_SINGLE | ASTRING_DOUBLE
bstring : "<" /(?P<KSON_MARKER>.*?)>.*?<\/(?P=KSON_MARKER)/ ">"

COMMENT: /#[^\n]*/
_NEWLINE: ( /\r?\n[\t ]*/ | COMMENT )+

WS: /[ \t\f]/+
%ignore WS
%ignore _NEWLINE

%import common._STRING_ESC_INNER
%import common._STRING_INNER
%import common.SIGNED_NUMBER
