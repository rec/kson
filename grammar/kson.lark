?start: value+

?value: object
      | array
      | string
      | astring
      | bstring
      | SIGNED_NUMBER      -> number
      | "true"             -> true
      | "false"            -> false
      | "null"             -> null

array  : "[" [ ","? value ("," value)* ","? ] "]"
object : "{" [ ","? pair ("," pair)* ","? ] "}"
pair   : string ":" value

ESCAPED_STRING_DOUBLE : "\"" _STRING_ESC_INNER "\""
ESCAPED_STRING_SINGLE : "'" _STRING_ESC_INNER "'"

string : ESCAPED_STRING_SINGLE | ESCAPED_STRING_DOUBLE

ASTRING_SINGLE : "a'" /.*?'/
ASTRING_DOUBLE : "a\"" /.*?\"/

astring : ASTRING_SINGLE | ASTRING_DOUBLE
bstring : "<" /(?P<KSON_MARKER>.*?)>.*?<\/(?P=KSON_MARKER)/ ">"

COMMENT: /#[^\n]*/
_NEWLINE: ( /\r?\n[\t ]*/ | COMMENT )+

WS: /[ \t\f]/+
%ignore WS
%ignore _NEWLINE


%import common._STRING_ESC_INNER
%import common._STRING_INNER
%import common.SIGNED_NUMBER
