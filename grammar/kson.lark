?start: value+

?value: object
      | array
      | string
      | astring
      | bstring
      | integer
      | floating
      | false
      | true
      | null
      | nan
      | inf
      | minus_inf

object : "{" [ ","? object_entry ("," object_entry)* ","? ] "}"
object_entry   : string ":" value

array  : "[" [ ","? value ("," value)* ","? ] "]"

string : STRING_SINGLE | STRING_DOUBLE
STRING_DOUBLE : "\"" _STRING_ESC_INNER "\""
STRING_SINGLE : "'" _STRING_ESC_INNER "'"

astring : ASTRING_SINGLE | ASTRING_DOUBLE
ASTRING_SINGLE : "a'" /.*?'/
ASTRING_DOUBLE : "a\"" /.*?\"/

bstring : "<" /(?P<KSON_MARKER>.*?)>.*?<\/(?P=KSON_MARKER)/ ">"

integer: SIGNED_INT
floating: SIGNED_FLOAT

true: "true"
false: "false"
null: "null"

nan : "nan" | "NaN"
inf : "inf" | "Infinity"
minus_inf : "-inf" | "-Infinity"

COMMENT: /#[^\n]*/
_NEWLINE: ( /\r?\n[\t ]*/ | COMMENT )+

WS: /[ \t\f]/+
%ignore WS
%ignore _NEWLINE

%import common._STRING_ESC_INNER
%import common._STRING_INNER
%import common.SIGNED_INT
%import common.SIGNED_FLOAT
